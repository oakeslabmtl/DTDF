vocabulary <https://bentleyjoakes.github.io/DTDF/vocab/DTDFVocab#> as DTDFVocab {
	
	extends <http://www.w3.org/2001/XMLSchema#> as xsd
	
	extends<https://bentleyjoakes.github.io/DTDF/vocab/base#> as base
	
	concept DigitalTwin < base:ContainedElement

	concept Model < base:ContainedElement  [
		restricts all base:isContainedIn to DigitalTwin
	]
	concept Data < base:ContainedElement  [
		restricts all base:isContainedIn to DigitalTwin
	]
	
	concept Enabler < base:ContainedElement  [
		restricts all base:isContainedIn to DigitalTwin
	]
	
	concept Service < base:ContainedElement  [
		restricts all base:isContainedIn to DigitalTwin
	]
	
	
	concept SystemUnderStudy
	relation entity HasSystem [
		from SystemUnderStudy
		to System
		forward hasSystem
	]
	relation entity HasEnvironment [
		from SystemUnderStudy
		to Environment
		forward hasEnvironment
	] 
	relation entity HasAgent [
		from SystemUnderStudy
		to Agent
		forward hasAgent
	]  
	
	concept System
	concept Environment
	concept Agent
	
	concept ActingComponent
	relation entity HasActingComponent [
		from SystemUnderStudy
		to ActingComponent
		forward hasActingComponent
	]

	concept SensingComponent
	relation entity HasSensingComponent [
		from SystemUnderStudy
		to SensingComponent
		forward hasSensingComponent
	]
	
	scalar property multiplicity [
		domain DigitalTwin, SystemUnderStudy
		range xsd:integer
		functional
	]
	
	concept DataTransmitted
	
	// relations from sensor to data transmitted
	// relations from data transmitted to data on DT side
	
	concept Insight
	concept Action
	
	scalar property IsAutomatic [
		domain DataTransmitted, Action
		range xsd:boolean
		functional
	]
	
	concept TimeScale < base:DescribedThing
	relation entity HasTimeScale [
		from DataTransmitted, Insight, Action, Enabler
		to TimeScale
		forward hasTimeScale
	]
	
	concept FidelityConsideration < base:DescribedThing
	
	concept LifeCycleStage < base:DescribedThing
	
	concept EvolutionStage < base:DescribedThing
	relation entity nextEvoStage [
		from EvolutionStage
		to EvolutionStage
		forward nextStage
		irreflexive
	]
}