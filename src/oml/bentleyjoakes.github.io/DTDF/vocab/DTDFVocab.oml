vocabulary <https://bentleyjoakes.github.io/DTDF/vocab/DTDFVocab#> as DTDFVocab {
	
	extends <http://www.w3.org/2001/XMLSchema#> as xsd
	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
	
	extends<https://bentleyjoakes.github.io/DTDF/vocab/base#> as base
	
	concept DigitalTwin
	
	//------------------------------
	// C1: SystemUnderStudy
	concept SystemUnderStudy
	relation entity HasSystem [
		from SystemUnderStudy
		to System
		forward hasSystem
	]
	relation entity HasEnvironment [
		from SystemUnderStudy
		to Environment
		forward hasEnvironment
	] 
	relation entity HasAgent [
		from SystemUnderStudy
		to Agent
		forward hasAgent
	]  
	
	concept System < base:ContainedElement
	scalar property Purpose [
		domain System, DigitalTwin
		range xsd:string 
	]
	
	concept Environment < base:DescribedThing, base:ContainedElement
	concept Agent < base:DescribedThing
	concept Component < base:DescribedThing, base:ContainedElement
	
	//------------------------------
	// C2: ActingComponents
	
	concept ActingComponent < Component

	//------------------------------
	// C3: SensingComponents
	concept SensingComponent < Component
	
	//------------------------------
	// C4: Multiplicity
	scalar property multiplicity [
		domain DigitalTwin, SystemUnderStudy
		range xsd:integer
		functional
	]
	
	//------------------------------
	// C5: Data Transmitted
	concept DataTransmitted
	
	// relations from sensor to data transmitted
	// relations from data transmitted to data on DT side
	relation entity DataInput [
		from DataTransmitted
		to Data
		forward asData
		reverse fromData
	]
	
	//------------------------------
	// C6: Insights/Actions
	concept Insight
	concept Action
	
	relation entity Provides [
		from Service
		to Insight, Action
		forward provides
		reverse providedBy
	]
	
	//------------------------------
	// C7: Services
	concept Service
	
	//------------------------------
	// C8: Enablers
	concept Enabler
	
	relation entity Enables [
		from Enabler
		to Service
		forward enables
		reverse enabledBy
	]
	
	scalar property IsCommEnabler [
		domain Enabler
		range xsd:boolean
		functional
	]
	
	//------------------------------
	// C9: Models/Data
	concept Model
	concept Data
	
	relation entity InputTo [
		from Model, Data
		to Enabler
		forward inputTo
		reverse hasInput
	]
	
	//------------------------------
	// C10: Constellation
	
	//------------------------------
	// C11: TimeScale
	concept TimeScale < base:DescribedThing
	relation entity HasTimeScale [
		from DataTransmitted, Insight, Action, Enabler
		to TimeScale
		forward hasTimeScale
	]
	
	//------------------------------
	// C12: FidelityConsideration
	concept FidelityConsideration < base:DescribedThing
	
	//------------------------------
	// C13: LifeCycleStage
	concept LifeCycleStage < base:DescribedThing
	relation entity AtStage [
		from Service
		to LifeCycleStage
		forward atStage
		reverse providedService
	]
	
	//------------------------------
	// C14: EvolutionStage
	concept EvolutionStage < base:DescribedThing
	relation entity nextEvoStage [
		from EvolutionStage
		to EvolutionStage
		forward nextStage
		irreflexive
	]
	
	//------------------
	// Multi-characteristic concepts
	scalar property IsAutomatic [
		domain DataTransmitted, Action
		range xsd:boolean
		functional
	]
}