vocabulary <https://bentleyjoakes.github.io/DTDF/vocab/base#> as base {
	
	extends <http://purl.org/dc/elements/1.1/> as dc
    extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
    extends <http://www.w3.org/2001/XMLSchema#> as xsd
	    
	@rdfs:label "IdentifiedThing"
	@dc:^description """An [=IdentifiedThing=] is a thing with identity and identifying properties."""
	aspect IdentifiedThing [
		key hasIdentifier
	]

	@rdfs:label "hasCanonicalName"
	scalar property hasCanonicalName [
		domain IdentifiedThing
		range xsd:string
	]

	@rdfs:label "hasIdentifier"
	scalar property hasIdentifier [
		domain IdentifiedThing
		range xsd:string
		functional
	]
	
	aspect DescribedThing
	scalar property desc [
		domain DescribedThing
		range xsd:string
		functional
	]
	
	@rdfs:comment "A ContainedElement is a thing that can participate in homomeric containment relationships."
	aspect ContainedElement
	
	@rdfs:comment "Contains is a many-to-many relation used to represent homomeric relations that form directed rooted trees."
	relation entity Contains [
		from ContainedElement
		to ContainedElement
		forward contains
		reverse isContainedIn
		inverse functional
		asymmetric
		irreflexive
	]
}